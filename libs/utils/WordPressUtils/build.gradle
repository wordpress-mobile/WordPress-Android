buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath 'com.novoda:bintray-release:0.8.1'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'

repositories {
    google()
    jcenter()
}

dependencies {
    implementation 'org.apache.commons:commons-text:1.1'
    implementation 'com.mcxiaoke.volley:library:1.0.18'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'org.greenrobot:eventbus:3.0.0'

    lintChecks 'org.wordpress:lint:1.0.1'
}

android {
    useLibrary 'org.apache.http.legacy'

    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        versionName "1.22"
        minSdkVersion 15
        targetSdkVersion 28
    }
}

android.libraryVariants.all { variant ->
    task("generate${variant.name}Javadoc", type: Javadoc) {
        description "Generates Javadoc for $variant.name."
        source = variant.javaCompile.source

        options {
            links "http://docs.oracle.com/javase/7/docs/api/"
        }
        exclude '**/R.java'
        doFirst {
            classpath =
                    files(variant.javaCompile.classpath.files,
                            project.android.getBootClasspath())
        }
    }
}

publish {
    artifactId = 'utils'
    userOrg = 'wordpress-mobile'
    groupId = 'org.wordpress'
    uploadName = 'utils'
    desc = 'Utils library for Android'
    publishVersion = android.defaultConfig.versionName
    licences = ['MIT', 'GPL']
    website = 'https://github.com/wordpress-mobile/WordPress-Utils-Android/'
    dryRun = 'false'
    autoPublish = 'true'
}
