buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.1'
        classpath 'com.novoda:bintray-release:0.9'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'

repositories {
    google()
    jcenter()
}

dependencies {
    implementation 'org.apache.commons:commons-text:1.1'
    implementation 'com.android.volley:volley:1.1.1'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'org.greenrobot:eventbus:3.0.0'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.assertj:assertj-core:3.11.1'

    lintChecks 'org.wordpress:lint:1.0.1'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test:rules:1.1.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
}

android {
    useLibrary 'org.apache.http.legacy'

    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        versionName "1.24"
        minSdkVersion 15
        targetSdkVersion 26

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
}

android.libraryVariants.all { variant ->
    task("generate${variant.name}Javadoc", type: Javadoc) {
        description "Generates Javadoc for $variant.name."
        source = variant.javaCompile.source

        options {
            links "http://docs.oracle.com/javase/7/docs/api/"
        }
        exclude '**/R.java'
        doFirst {
            classpath =
                    files(variant.javaCompile.classpath.files,
                            project.android.getBootClasspath())
        }
    }
}

publish {
    artifactId = 'utils'
    userOrg = 'wordpress-mobile'
    groupId = 'org.wordpress'
    uploadName = 'utils'
    desc = 'Utils library for Android'
    publishVersion = android.defaultConfig.versionName
    licences = ['MIT', 'GPL']
    website = 'https://github.com/wordpress-mobile/WordPress-Utils-Android/'
    dryRun = 'false'
    autoPublish = 'true'
}
