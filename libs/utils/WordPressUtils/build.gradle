buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.0.0-beta7'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    jcenter()
}

dependencies {
    compile('commons-lang:commons-lang:2.6') {
        exclude group: 'commons-logging'
    }
    compile 'com.mcxiaoke.volley:library:1.0.18'
    compile 'com.android.support:support-v13:23.1.1'
}

android {
    useLibrary 'org.apache.http.legacy'

    publishNonDefault true

    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        versionName "1.9.0"
        minSdkVersion 14
        targetSdkVersion 23
    }
}

version android.defaultConfig.versionName
group = "org.wordpress"
archivesBaseName = "utils"

signing {
    required {
        has("release") && project.properties.containsKey("signing.keyId") && project.properties.containsKey("signing.secretKeyRingFile")
    }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: project.properties.ossrhUsername, password: project.properties.ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: project.properties.ossrhUsername, password: project.properties.ossrhPassword)
            }

            pom.project {
                name 'WordPress-Utils-Android'
                packaging 'aar'
                description 'Utils lib for WordPress-Android'
                url 'https://github.com/wordpress-mobile/WordPress-Utils-Android'
                scm {
                    connection 'scm:git:https://github.com/wordpress-mobile/WordPress-Utils-Android.git'
                    developerConnection 'scm:git:https://github.com/wordpress-mobile/WordPress-Utils-Android.git'
                    url 'https://github.com/wordpress-mobile/WordPress-Utils-Android'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'http://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'maxme'
                        name 'Maxime Biais'
                        email 'maxime@automattic.com'
                    }
                }
            }
        }
    }
}

android.libraryVariants.all { variant ->

    task("generate${variant.name}Javadoc", type: Javadoc) {
        description "Generates Javadoc for $variant.name."
        source = variant.javaCompile.source
        classpath = files(variant.javaCompile.classpath.files, android.getBootClasspath())

        options {
            links "http://docs.oracle.com/javase/7/docs/api/"
        }
        exclude '**/R.java'
    }
}
