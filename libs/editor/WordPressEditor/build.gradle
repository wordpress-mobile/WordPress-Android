buildscript {
    repositories {
        google()
        jcenter()
    }

    ext {
        aztecVersion = 'v1.3.36'
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.1'
    }
}

repositories {
    google()
    jcenter()
    maven { url "https://www.jitpack.io" }
    maven { url "https://dl.bintray.com/wordpress-mobile/react-native-mirror/" }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'


// import the `submoduleGitHash()` function
apply from: 'https://gist.githubusercontent.com/hypest/e06f6097065728b6db7b7c462f8fef1a/raw/3b91756fca76e4c2a9b573313e186c47842e1f7d/submoduleGitHash.gradle'

// import the `waitJitpack()` function
apply from: 'https://gist.githubusercontent.com/hypest/f526fe0775dedce0ce0133f1400d22a4/raw/0008b271a0d28fc79957fd3c2a027f57e98f796a/wait-jitpack.gradle'

android {
    compileSdkVersion 28

    defaultConfig {
        versionCode 13
        versionName "1.3"
        minSdkVersion 21
        targetSdkVersion 28
        buildConfigField "boolean", "BUILD_GUTENBERG_FROM_SOURCE", (rootProject.ext.has("buildGutenbergFromSource") ? rootProject.ext.buildGutenbergFromSource : false).toString()
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // Avoid 'duplicate files during packaging of APK' errors
    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.vectordrawable:vectordrawable-animated:1.0.0'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'org.apache.commons:commons-lang3:3.5'
    implementation 'com.android.volley:volley:1.1.1'
    implementation 'org.wordpress:utils:1.22'

    api ("com.github.wordpress-mobile.WordPress-Aztec-Android:aztec:$aztecVersion") {
        exclude group: "com.android.volley"
    }
    api ("com.github.wordpress-mobile.WordPress-Aztec-Android:wordpress-shortcodes:$aztecVersion")
    api ("com.github.wordpress-mobile.WordPress-Aztec-Android:wordpress-comments:$aztecVersion")

    if (rootProject.ext.has("buildGutenbergFromSource") && rootProject.ext.buildGutenbergFromSource) {
        implementation (project(':react-native-gutenberg-bridge')) {
            exclude group: 'com.squareup.okhttp3'
            exclude group: "com.android.volley"
            exclude group: 'com.google.code.findbugs'
        }
    } else {
        implementation (waitJitpack('com.github.wordpress-mobile', 'gutenberg-mobile', submoduleGitHash('../../../', 'libs/gutenberg-mobile'))) {
            // Inheriting RN's okhttp version can interfere with FluxC SSL handling
            // See https://github.com/wordpress-mobile/WordPress-Android/issues/9032 and https://github.com/wordpress-mobile/WordPress-FluxC-Android/issues/919
            exclude group: 'com.squareup.okhttp3'
            exclude group: "com.android.volley"
            exclude group: 'com.google.code.findbugs'
        }
    }

    // Required Aztec dependencies (they should be included but Jitpack seems to be stripping these out)
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.1.4"
    implementation "org.jsoup:jsoup:1.10.3"

    implementation 'com.google.code.gson:gson:2.6.2'

    lintChecks 'org.wordpress:lint:1.0.1'
}

signing {
    required {
        project.properties.containsKey("signing.keyId") && project.properties.containsKey("signing.secretKeyRingFile")
    }
    sign configurations.archives
}

version android.defaultConfig.versionName
group = "org.wordpress"
archivesBaseName = "editor"

// http://central.sonatype.org/pages/gradle.html

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: project.properties.ossrhUsername, password: project.properties.ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: project.properties.ossrhUsername, password: project.properties.ossrhPassword)
            }

            pom.project {
                name 'WordPress-Android-Editor'
                packaging 'aar'
                description 'A reusable Android rich text editor component'
                url 'https://github.com/wordpress-mobile/WordPress-Android-Editor'
                scm {
                    connection 'scm:git:https://github.com/wordpress-mobile/WordPress-Android-Editor.git'
                    developerConnection 'scm:git:https://github.com/wordpress-mobile/WordPress-Android-Editor.git'
                    url 'https://github.com/wordpress-mobile/WordPress-Android-Editor'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'http://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'maxme'
                        name 'Maxime Biais'
                        email 'maxime@automattic.com'
                    }
                }
            }
        }
    }
}
