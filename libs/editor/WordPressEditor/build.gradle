buildscript {
    repositories {
        google()
        jcenter()
    }

    ext {
        aztecVersion = 'v1.3.45'
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

repositories {
    google()
    jcenter()
    maven { url "https://www.jitpack.io" }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def buildGutenbergFromSource = gradle.ext.includedBuildGutenbergMobilePath != null

android {
    compileSdkVersion 29

    defaultConfig {
        versionCode 13
        versionName "1.3"
        minSdkVersion 21
        targetSdkVersion 29
        buildConfigField "boolean", "BUILD_GUTENBERG_FROM_SOURCE", buildGutenbergFromSource.toString()
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // Avoid 'duplicate files during packaging of APK' errors
    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.vectordrawable:vectordrawable-animated:1.0.0'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'org.apache.commons:commons-lang3:3.5'
    implementation 'com.android.volley:volley:1.1.1'
    implementation 'org.wordpress:utils:1.25'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    api ("com.github.wordpress-mobile.WordPress-Aztec-Android:aztec:$aztecVersion") {
        exclude group: "com.android.volley"
    }
    api ("com.github.wordpress-mobile.WordPress-Aztec-Android:wordpress-shortcodes:$aztecVersion")
    api ("com.github.wordpress-mobile.WordPress-Aztec-Android:wordpress-comments:$aztecVersion")

    // This dependency will be substituted if `local-builds.gradle` file contains
    // `localGutenbergMobilePath`. Details for this can be found in the `settings.gradle` file.
    implementation "$rootProject.gradle.ext.gutenbergMobileBinaryPath:$rootProject.ext.gutenbergMobileVersion"

    // Required Aztec dependencies (they should be included but Jitpack seems to be stripping these out)
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.1.4"
    implementation "org.jsoup:jsoup:1.10.3"

    implementation 'com.google.code.gson:gson:2.6.2'

    lintChecks 'org.wordpress:lint:1.0.1'

    // We need to add the hermes library manually for binary dependency.
    // There doesn't seem to be a good way to integrate an aar into a library dependency, and it's
    // not something Google wants to support, so this is a necessary workaround.
    if (!buildGutenbergFromSource) {
        def hermesPath = './libs/hermes/'
        debugRuntimeOnly files(hermesPath + 'hermes-debug.aar')
        releaseRuntimeOnly files(hermesPath + 'hermes-release.aar')
    }
}

signing {
    required {
        project.properties.containsKey("signing.keyId") && project.properties.containsKey("signing.secretKeyRingFile")
    }
    sign configurations.archives
}

version android.defaultConfig.versionName
group = "org.wordpress"
archivesBaseName = "editor"

// http://central.sonatype.org/pages/gradle.html

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: project.properties.ossrhUsername, password: project.properties.ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: project.properties.ossrhUsername, password: project.properties.ossrhPassword)
            }

            pom.project {
                name 'WordPress-Android-Editor'
                packaging 'aar'
                description 'A reusable Android rich text editor component'
                url 'https://github.com/wordpress-mobile/WordPress-Android-Editor'
                scm {
                    connection 'scm:git:https://github.com/wordpress-mobile/WordPress-Android-Editor.git'
                    developerConnection 'scm:git:https://github.com/wordpress-mobile/WordPress-Android-Editor.git'
                    url 'https://github.com/wordpress-mobile/WordPress-Android-Editor'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'http://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'maxme'
                        name 'Maxime Biais'
                        email 'maxime@automattic.com'
                    }
                }
            }
        }
    }
}
