buildscript {
    ext.aztecVersion = 'v1.5.3'
}

repositories {
    maven {
        url "https://a8c-libs.s3.amazonaws.com/android"
        content {
            includeGroup "org.wordpress"
            includeGroup "org.wordpress.aztec"
            includeGroup "org.wordpress-mobile.gutenberg-mobile"
        }
    }
    maven {
        url "https://a8c-libs.s3.amazonaws.com/android/react-native-mirror"
        content {
            includeGroup "com.facebook.react"
        }
    }
    maven {
        url "https://a8c-libs.s3.amazonaws.com/android/hermes-mirror"
        content {
            includeGroup "org.wordpress-mobile"
        }
    }
    google()
    mavenCentral()
    jcenter {
        content {
            includeVersion "com.android.volley", "volley", "1.1.1"
            includeVersion "org.wordpress", "utils", "1.25"
        }
    }
    maven { url "https://www.jitpack.io" }
    maven { url "https://a8c-libs.s3.amazonaws.com/android/hermes-mirror" }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // Avoid 'duplicate files during packaging of APK' errors
    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.vectordrawable:vectordrawable-animated:1.0.0'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'org.apache.commons:commons-lang3:3.5'
    implementation 'com.android.volley:volley:1.1.1'
    implementation 'org.wordpress:utils:1.25'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    api ("$gradle.ext.aztecAndroidAztecPath:$aztecVersion") {
        exclude group: "com.android.volley"
    }
    api ("$gradle.ext.aztecAndroidWordPressShortcodesPath:$aztecVersion")
    api ("$gradle.ext.aztecAndroidWordPressCommentsPath:$aztecVersion")

    // This dependency will be substituted if the `local-builds.gradle` file contains
    // `localGutenbergMobilePath`. Details for this can be found in the `settings.gradle` file.
    implementation "$rootProject.gradle.ext.gutenbergMobileBinaryPath:$rootProject.ext.gutenbergMobileVersion"

    // Required Aztec dependencies (they should be included but Jitpack seems to be stripping these out)
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.1.4"
    implementation "org.jsoup:jsoup:1.10.3"

    implementation 'com.google.code.gson:gson:2.6.2'

    lintChecks 'org.wordpress:lint:1.0.1'
}

