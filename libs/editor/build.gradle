plugins {
    id "com.android.library"
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.parcelize)
    id "org.jetbrains.kotlinx.kover"
}

repositories {
    maven {
        url "https://a8c-libs.s3.amazonaws.com/android"
        content {
            includeGroup "org.wordpress"
            includeGroup "org.wordpress.aztec"
            includeGroup "org.wordpress.gutenberg-mobile"
            includeGroupByRegex "org.wordpress.react-native-libraries.*"
            includeGroup "com.automattic"
            includeGroup "com.automattic.tracks"
        }
    }
    maven {
        url "https://a8c-libs.s3.amazonaws.com/android/react-native-mirror"
        content {
            includeGroup "com.facebook.react"
        }
    }
    google()
    mavenCentral()
    maven { url "https://www.jitpack.io" }
}

android {
    namespace "org.wordpress.android.editor"

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        compileSdk rootProject.compileSdkVersion
    }

    lint {
        enable += 'UnknownNullness'
    }

    // Avoid 'duplicate files during packaging of APK' errors
    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }

    buildFeatures {
        buildConfig true
    }
}

dependencies {
    implementation("$gradle.ext.wputilsBinaryPath") {
        version {
            strictly libs.wordpress.utils.get().version
        }
    }
    api("$gradle.ext.aztecAndroidAztecPath") {
        exclude group: "com.android.volley"
        exclude group: 'org.wordpress', module: 'utils'
        version {
            strictly libs.versions.wordpress.aztec.get()
        }
    }
    api("$gradle.ext.aztecAndroidWordPressShortcodesPath:${libs.versions.wordpress.aztec.get()}") {
        version {
            strictly libs.versions.wordpress.aztec.get()
        }
    }
    api("$gradle.ext.aztecAndroidWordPressCommentsPath:${libs.versions.wordpress.aztec.get()}") {
        version {
            strictly libs.versions.wordpress.aztec.get()
        }
    }

    implementation(libs.facebook.react.android.hermes)
    implementation(libs.facebook.react.android.main)

    // This dependency will be substituted if the `local-builds.gradle` file contains
    // `localGutenbergMobilePath`. Details for this can be found in the `settings.gradle` file.
    implementation("$rootProject.gradle.ext.gutenbergMobileBinaryPath:${libs.versions.gutenberg.mobile.get()}") {
        // We exclude React Native modules from older and newer versions.
        // This is needed to avoid Duplicate class errors when building using local Gutenberg Mobile path.
        exclude module: 'hermes-engine'
        exclude module: 'react-native'
        exclude module: 'hermes-android'
        exclude module: 'react-android'
    }

    implementation(libs.wordpress.utils)

    implementation(libs.androidx.lifecycle.common)
    implementation(libs.androidx.lifecycle.livedata.core)
    implementation(libs.androidx.core.main)
    implementation(libs.androidx.fragment.main)
    implementation(libs.androidx.appcompat.main)
    implementation(libs.androidx.preference)
    implementation(libs.google.material)
    implementation(libs.android.volley)
    implementation(libs.google.gson)
    implementation(libs.automattic.tracks.crashlogging)

    lintChecks(libs.wordpress.lint)
}
