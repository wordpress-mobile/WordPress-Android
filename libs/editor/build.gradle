plugins {
    id "com.android.library"
    id "org.jetbrains.kotlin.android"
    id "org.jetbrains.kotlin.plugin.parcelize"
    id "org.jetbrains.kotlinx.kover"
}

repositories {
    maven {
        url "https://a8c-libs.s3.amazonaws.com/android"
        content {
            includeGroup "org.wordpress"
            includeGroup "org.wordpress.aztec"
            includeGroup "org.wordpress.gutenberg-mobile"
            includeGroupByRegex "org.wordpress.react-native-libraries.*"
            includeGroup "com.automattic"
            includeGroup "com.automattic.tracks"
        }
    }
    maven {
        url "https://a8c-libs.s3.amazonaws.com/android/react-native-mirror"
        content {
            includeGroup "com.facebook.react"
        }
    }
    google()
    mavenCentral()
    maven { url "https://www.jitpack.io" }
}

android {
    namespace "org.wordpress.android.editor"

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        compileSdk rootProject.compileSdkVersion
    }

    lint {
        enable += 'UnknownNullness'
    }

    // Avoid 'duplicate files during packaging of APK' errors
    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }

    buildFeatures {
        buildConfig true
    }
}

dependencies {
    implementation("$gradle.ext.wputilsBinaryPath") {
        version {
            strictly libs.wordpress.utils.get().version
        }
    }
    api ("$gradle.ext.aztecAndroidAztecPath") {
        exclude group: "com.android.volley"
        exclude group: 'org.wordpress', module: 'utils'
        version {
            strictly wordPressAztecVersion
        }
    }
    api ("$gradle.ext.aztecAndroidWordPressShortcodesPath:$wordPressAztecVersion") {
        version {
            strictly wordPressAztecVersion
        }
    }
    api ("$gradle.ext.aztecAndroidWordPressCommentsPath:$wordPressAztecVersion") {
        version {
            strictly wordPressAztecVersion
        }
    }

    implementation "com.facebook.react:hermes-android:$facebookReactVersion"
    implementation "com.facebook.react:react-android:$facebookReactVersion"

    // This dependency will be substituted if the `local-builds.gradle` file contains
    // `localGutenbergMobilePath`. Details for this can be found in the `settings.gradle` file.
    implementation ("$rootProject.gradle.ext.gutenbergMobileBinaryPath:$rootProject.ext.gutenbergMobileVersion") {
        // We exclude React Native modules from older and newer versions.
        // This is needed to avoid Duplicate class errors when building using local Gutenberg Mobile path.
        exclude module: 'hermes-engine'
        exclude module: 'react-native'
        exclude module: 'hermes-android'
        exclude module: 'react-android'
    }

    implementation(libs.wordpress.utils)

    implementation "androidx.lifecycle:lifecycle-common:$androidxLifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-core:$androidxLifecycleVersion"
    implementation "androidx.core:core:$androidxCoreVersion"
    implementation "androidx.fragment:fragment:$androidxFragmentVersion"
    implementation "androidx.appcompat:appcompat:$androidxAppcompatVersion"
    implementation "androidx.preference:preference:$androidxPreferenceVersion"
    implementation "com.google.android.material:material:$googleMaterialVersion"
    implementation(libs.android.volley)
    implementation "com.google.code.gson:gson:$googleGsonVersion"
    implementation "com.automattic.tracks:crashlogging:$automatticTracksVersion"

    lintChecks(libs.wordpress.lint)
}
