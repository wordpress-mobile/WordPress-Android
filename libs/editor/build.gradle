plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.parcelize)
    alias(libs.plugins.kotlix.kover)
}

repositories {
    maven {
        url "https://a8c-libs.s3.amazonaws.com/android"
        content {
            includeGroup "org.wordpress"
            includeGroup "org.wordpress.aztec"
            includeGroup "org.wordpress.gutenberg-mobile"
            includeGroupByRegex "org.wordpress.react-native-libraries.*"
            includeGroup "com.automattic"
            includeGroup "com.automattic.tracks"
        }
    }
    maven {
        url "https://a8c-libs.s3.amazonaws.com/android/react-native-mirror"
        content {
            includeGroup "com.facebook.react"
        }
    }
    google()
    mavenCentral()
    maven { url "https://www.jitpack.io" }
}

android {
    namespace "org.wordpress.android.editor"

    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
    }

    lint {
        enable += 'UnknownNullness'
    }

    // Avoid 'duplicate files during packaging of APK' errors
    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }

    buildFeatures {
        buildConfig true
    }
}

dependencies {
    implementation ("$gradle.ext.wputilsBinaryPath") {
        version {
            strictly libs.versions.wordPressUtils.get()
        }
    }
    api ("$gradle.ext.aztecAndroidAztecPath") {
        exclude group: "com.android.volley"
        exclude group: 'org.wordpress', module: 'utils'
        version {
            strictly libs.versions.wordPressAztec.get()
        }
    }
    // TODO: Migrate from composite build path modules to version catalogs (libs.version.toml).
    // noinspection UseTomlInstead
    api ("$gradle.ext.aztecAndroidWordPressShortcodesPath:${libs.versions.wordPressAztec.get()}") {
        version {
            strictly libs.versions.wordPressAztec.get()
        }
    }
    // TODO: Migrate from composite build path modules to version catalogs (libs.version.toml).
    // noinspection UseTomlInstead
    api ("$gradle.ext.aztecAndroidWordPressCommentsPath:${libs.versions.wordPressAztec.get()}") {
        version {
            strictly libs.versions.wordPressAztec.get()
        }
    }

    implementation libs.facebook.react.hermesAndroid
    implementation libs.facebook.react.android

    // This dependency will be substituted if the `local-builds.gradle` file contains
    // `localGutenbergMobilePath`. Details for this can be found in the `settings.gradle` file.
    // TODO: Migrate from composite build path modules to version catalogs (libs.version.toml).
    // noinspection UseTomlInstead
    implementation ("$rootProject.gradle.ext.gutenbergMobileBinaryPath:${libs.versions.gutenbergMobile.get()}") {
        // We exclude React Native modules from older and newer versions.
        // This is needed to avoid Duplicate class errors when building using local Gutenberg Mobile path.
        exclude module: 'hermes-engine'
        exclude module: 'react-native'
        exclude module: 'hermes-android'
        exclude module: 'react-android'
    }

    implementation libs.wordpress.utils

    implementation libs.androidx.lifecycle.common
    implementation libs.androidx.lifecycle.runtime
    implementation libs.androidx.lifecycle.livedata.core
    implementation libs.androidx.core
    implementation libs.androidx.fragment
    implementation libs.androidx.appcompat
    implementation libs.androidx.preference
    implementation libs.google.material
    implementation libs.volley
    implementation libs.google.gson
    implementation libs.automattic.tracks.crashlogging

    lintChecks libs.wordpress.lint
}
