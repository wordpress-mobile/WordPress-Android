plugins {
    id "com.android.library"
}

android {
    namespace "org.wordpress.android.mocks"

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        compileSdk rootProject.compileSdkVersion
    }

    lint {
        enable += 'UnknownNullness'
    }
}

dependencies {
    api(libs.wiremock.get().module.toString()) {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
        exclude group: 'org.apache.commons', module: 'commons-lang3'
        exclude group: 'asm', module: 'asm'
        exclude group: 'org.json', module: 'json'
    }
    runtimeOnly(libs.apache.http.client.android)

    constraints {
        implementation(libs.wiremock) {
            because("newer versions of WireMock use Java APIs not available on Android")
        }

        def wireMockSecurity = "version shipped with WireMock 2.26.3 contains security vulnerabilities"

        implementation('org.eclipse.jetty:jetty-webapp:9.4.51.v20230217') {
            because(wireMockSecurity)
        }
        implementation('com.fasterxml.jackson.core:jackson-databind:2.12.7.1') {
            because(wireMockSecurity)
        }
        implementation('com.jayway.jsonpath:json-path:2.9.0') {
            because(wireMockSecurity)
        }
        implementation('commons-fileupload:commons-fileupload:1.5') {
            because(wireMockSecurity)
        }
    }
}
