plugins {
    id "com.android.library"
    id "org.jetbrains.kotlin.android"
    id "org.jetbrains.kotlin.plugin.parcelize"
    id "androidx.navigation.safeargs.kotlin"
    id "org.jetbrains.kotlinx.kover"
}

android {
    namespace "org.wordpress.android.imageeditor"

    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
        }
    }

    lint {
        enable += 'UnknownNullness'
    }

    buildFeatures {
        viewBinding true
    }
}

repositories {
    maven { url "https://www.jitpack.io" }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "androidx.core:core:$androidxCoreVersion"
    implementation "androidx.activity:activity:$androidxActivityVersion"
    implementation "androidx.activity:activity-ktx:$androidxActivityVersion"
    implementation "androidx.fragment:fragment:$androidxFragmentVersion"
    implementation "androidx.appcompat:appcompat:$androidxAppcompatVersion"
    implementation "androidx.recyclerview:recyclerview:$androidxRecyclerviewVersion"
    implementation "androidx.constraintlayout:constraintlayout:$androidxConstraintlayoutVersion"
    implementation "androidx.viewpager2:viewpager2:$androidxViewpager2Version"
    implementation "com.google.android.material:material:$googleMaterialVersion"
    implementation "androidx.navigation:navigation-fragment:$gradle.ext.navigationVersion"
    implementation "androidx.navigation:navigation-ui:$gradle.ext.navigationVersion"
    implementation "androidx.lifecycle:lifecycle-common:$androidxLifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-runtime:$androidxLifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel:$androidxLifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$androidxLifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-core:$androidxLifecycleVersion"
    implementation ("com.github.yalantis:ucrop:$uCropVersion") {
        exclude group: 'com.squareup.okhttp3'
        exclude group: 'androidx.core', module: 'core'
        exclude group: 'androidx.constraintlayout', module: 'constraintlayout'
        exclude group: 'androidx.appcompat', module: 'appcompat'
    }

    testImplementation "org.mockito.kotlin:mockito-kotlin:$mockitoKotlinVersion"
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.assertj:assertj-core:$assertjVersion"
    testImplementation("androidx.arch.core:core-testing:$androidxArchCoreVersion", {
        exclude group: 'com.android.support', module: 'support-compat'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-core-utils'
    })

    androidTestImplementation "androidx.test.ext:junit:$androidxTestExtJunitVersion"
    androidTestImplementation("androidx.test.espresso:espresso-core:$androidxTestEspressoVersion") {
        version {
            strictly androidxTestEspressoVersion
        }
    }
}
