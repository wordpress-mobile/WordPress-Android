buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
        classpath 'org.robolectric:robolectric-gradle-plugin:0.14.1'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'robolectric'
apply plugin: 'maven'
apply plugin: 'signing'

version = "1.0.0"
group = "org.wordpress"
archivesBaseName = "mediapicker"

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.android.support:support-v13:21.0.2'
    compile 'com.mcxiaoke.volley:library:1.0.10'

    androidTestCompile 'junit:junit:4.10'
    androidTestCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'org.robolectric:robolectric:2.4'

    // Workaround for IDE bug
    // http://stackoverflow.com/questions/22246183/android-studio-doesnt-recognize-espresso-classes
    provided 'junit:junit:4.10'
    provided 'org.mockito:mockito-core:1.10.19'
    provided 'org.robolectric:robolectric:2.4'
}

android {
    publishNonDefault true

    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 2
        versionName "1.0.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

robolectric {
    include '**/*Test.class'
}

apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.7.1.201405082137"
}

def coverageSourceDirs = [
    'src/main/java'
]

task jacocoTestReport(type: JacocoReport, dependsOn: "testDebug") {
    group = "Reporting"
    description = "Generate Jacoco coverage reports"

    classDirectories = fileTree(
            dir: 'build/intermediates/classes/debug',
            excludes: ['**/R.class',
                       '**/R$*.class',
                       '**/*$ViewInjector*.*',
                       '**/BuildConfig.*',
                       '**/Manifest*.*']
    )

    additionalSourceDirs = files(coverageSourceDirs)
    sourceDirectories = files(coverageSourceDirs)
    executionData = files('build/jacoco/testDebug.exec')

    reports {
        xml.enabled = true
        html.enabled = true
    }
}

signing {
    required {
        project.properties.containsKey("signing.keyId") && project.properties.containsKey("signing.secretKeyRingFile")
    }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: project.properties.ossrhUsername, password: project.properties.ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: project.properties.ossrhUsername, password: project.properties.ossrhPassword)
            }

            pom.project {
                name 'MediaPicker'
                packaging 'aar'
                description 'MediaPicker is an Android Fragment that enables users to select multiple media from a variety of sources.'
                url 'https://github.com/wordpress-mobile/MediaPicker'

                scm {
                    url 'https://github.com/wordpress-mobile/MediaPicker'
                    connection 'scm:git:https://github.com/wordpress-mobile/MediaPicker.git'
                    developerConnection 'scm:git:https://github.com/wordpress-mobile/MediaPicker.git'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'http://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'tonyr59h'
                        name 'Anthony Rankin'
                        email 'tonyr@automattic.com'
                    }
                }
            }
        }
    }
}
