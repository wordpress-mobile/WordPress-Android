buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.0'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'checkstyle'

repositories {
    jcenter()
}

android {
    compileSdkVersion 24
    buildToolsVersion "23.0.2"

    defaultConfig {
        versionCode 4
        versionName "0.1"
        minSdkVersion 14
        targetSdkVersion 24
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    // WordPress libs
    compile ('org.wordpress:utils:1.14.0') {
        // Using official volley package
        exclude group: "com.mcxiaoke.volley";
    }

    // Custom WellSql version
    compile 'org.wordpress:wellsql:1.1.0'
    apt 'org.wordpress:wellsql-processor:1.1.0'

    // FluxC annotations
    compile project(':fluxc-annotations')
    apt project(':fluxc-processor')

    // External libs
    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.squareup.okhttp3:okhttp-urlconnection:3.4.1'
    compile 'com.android.volley:volley:1.0.0'
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.google.dagger:dagger:2.0.2'
    apt 'com.google.dagger:dagger-compiler:2.0.2'
    provided 'org.glassfish:javax.annotation:10.0-b28'
}

task checkstyle(type: Checkstyle) {
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    classpath = files()
}

checkstyle {
    toolVersion = '6.9'
    configFile file("${project.rootDir}/config/checkstyle.xml")
}

version = android.defaultConfig.versionName
group = "org.wordpress"
