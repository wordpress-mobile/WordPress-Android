buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/repo' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
        classpath 'com.github.nrudenko:gradle-android-cq-plugin:0.1+'
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

repositories {
    mavenCentral()
    maven { url 'http://wordpress-mobile.github.io/WordPress-Android' }
    maven { url 'https://maven.fabric.io/repo' }
}

apply plugin: 'com.android.application'
apply plugin: 'android-cq'
apply plugin: 'io.fabric'

android {
    packagingOptions {
        exclude "META-INF/LICENSE.txt"
        exclude "META-INF/NOTICE.txt"
    }

    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    defaultConfig {
        applicationId "org.wordpress.android"
        versionName "3.9-rc-2"
        versionCode 172
        minSdkVersion 14
        targetSdkVersion 21

        buildConfigField "String", "APP_PN_KEY", "\"org.wordpress.android.playstore\""
    }

    productFlavors {
        vanilla {}

        zbetagroup {
            buildConfigField "String", "APP_PN_KEY", "\"org.wordpress.android.beta.build\""
            applicationId "org.wordpress.android.beta"
        }
    }

    buildTypes {
        release {
            // Proguard is only used to fix an issue with some Samsung device
            // https://github.com/wordpress-mobile/WordPress-Android/issues/2151
            minifyEnabled true
            proguardFile 'proguard.cfg'
        }

        debug {
            buildConfigField "String", "APP_PN_KEY", "\"org.wordpress.android.debug.build\""
            ext.enableCrashlytics = false
        }
    }
}

dependencies {
    compile 'com.crashlytics.sdk.android:crashlytics:2.2.2'

    // Provided by maven central
    compile ('org.wordpress:mediapicker:1.2.0') {
        exclude group:'com.android.support'
    }
    compile 'com.google.code.gson:gson:2.2.2'
    compile 'org.ccil.cowan.tagsoup:tagsoup:1.2.1'
    compile 'com.android.support:support-v13:21.0.+'
    compile 'com.android.support:appcompat-v7:21.0.+'
    compile 'com.android.support:cardview-v7:21.0.+'
    compile 'com.android.support:recyclerview-v7:21.0.+'
    compile 'com.github.chrisbanes.photoview:library:1.2.3'
    compile 'com.helpshift:android-aar:3.7.2'
    compile 'commons-lang:commons-lang:2.6'
    compile 'com.cocosw:undobar:1.6@aar'
    compile 'de.greenrobot:eventbus:2.4.0'

    compile 'com.mcxiaoke.volley:library:1.0.+'

    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.0'
    androidTestCompile 'org.objenesis:objenesis:2.1'
    androidTestCompile 'org.mockito:mockito-core:+'

    // Provided by the WordPress-Android Repository
    compile 'org.wordpress:gcm:1.0.0' // not found in maven central
    compile 'org.wordpress:drag-sort-listview:0.6.1' // not found in maven central
    compile 'org.wordpress:slidinguppanel:1.0.0' // not found in maven central
    compile 'org.wordpress:android-passcodelock:0.0.6'
    compile 'org.wordpress:emailchecker:0.3'

    // Simperium
    compile 'com.simperium.android:simperium:0.6.2'

    releaseCompile project(path:':libs:utils:WordPressUtils', configuration: 'release')
    debugCompile project(path:':libs:utils:WordPressUtils', configuration: 'debug')
    releaseCompile project(path:':libs:networking:WordPressNetworking', configuration: 'release')
    debugCompile project(path:':libs:networking:WordPressNetworking', configuration: 'debug')
    releaseCompile project(path:':libs:wpcomrest:WordPressComRest', configuration: 'release')
    debugCompile project(path:':libs:wpcomrest:WordPressComRest', configuration: 'debug')
    releaseCompile project(path:':libs:graphview:WordPressGraphView', configuration: 'release')
    debugCompile project(path:':libs:graphview:WordPressGraphView', configuration: 'debug')
    releaseCompile project(path:':libs:persistentedittext:PersistentEditText', configuration: 'release')
    debugCompile project(path:':libs:persistentedittext:PersistentEditText', configuration: 'debug')
    releaseCompile project(path:':libs:analytics:WordPressAnalytics', configuration: 'release')
    debugCompile project(path:':libs:analytics:WordPressAnalytics', configuration: 'debug')
    releaseCompile project(path:':libs:editor:WordPressEditor', configuration: 'release')
    debugCompile project(path:':libs:editor:WordPressEditor', configuration: 'debug')
}

configurations.all {
    // Exclude packaged wordpress sub projects, force the use of the source project
    // (eg. use :libs:utils:WordPressUtils instead of 'org.wordpress:utils')
    exclude group: 'org.wordpress', module: 'utils'
    exclude group: 'org.wordpress', module: 'analytics'
}

task generateCrashlyticsConfig(group: "generate", description: "Generate Crashlytics config") {
    def outputFile = new File("${rootDir}/WordPress/crashlytics.properties")
    def inputFile = file("${rootDir}/WordPress/gradle.properties")
    if (!inputFile.exists()) {
        throw new StopActionException("Build configuration file:" + inputFile
                + " doesn't exist, follow README instructions")
    }
    outputs.file outputFile
    inputs.file inputFile
    doLast {
        def properties = new Properties()
        inputFile.withInputStream { stream ->
            properties.load(stream)
        }
        def crashlyticsApiKey = properties.getProperty('crashlytics.apikey', '0')
        def writer = new FileWriter(outputFile)
        writer.write("""// auto-generated file from ${rootDir}/gradle.properties do not modify
apiKey=${crashlyticsApiKey}""")
        writer.close()
    }
}

// Add generateCrashlyticsConfig to all generateBuildConfig tasks (all variants)
android.applicationVariants.all { variant ->
    variant.generateBuildConfig.dependsOn(generateCrashlyticsConfig)
}

// Add properties named "wp.xxx" to our BuildConfig
android.buildTypes.all { buildType ->
    project.properties.any { property ->
        if (property.key.toLowerCase().startsWith("wp.")) {
            buildType.buildConfigField "String", property.key.replace("wp.", "").replace(".", "_").toUpperCase(),
                    "\"${property.value}\""
        }
    }
}

// For app signing
if (["storeFile", "storePassword", "keyAlias", "keyPassword"].count { !project.hasProperty(it) } == 0) {
    android {
        signingConfigs {
            release {
                storeFile = file(project.storeFile)
                storePassword = project.storePassword
                keyAlias = project.keyAlias
                keyPassword = project.keyPassword
            }
        }
    }
    android.buildTypes.release.signingConfig = android.signingConfigs.release
}
