plugins {
    id "java"
    alias(sharedLibs.plugins.kotlin.jvm)
    alias(sharedLibs.plugins.kotlin.kapt)
    alias(sharedLibs.plugins.automattic.publishToS3)
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    withSourcesJar()
    withJavadocJar()
}

dependencies {
    implementation fluxcAnnotationsProjectDependency
    implementation sharedLibs.google.autoService
    kapt sharedLibs.google.autoService
    implementation sharedLibs.squareup.javapoet
}

dependencyAnalysis {
    issues {
        onUnusedDependencies {
            exclude("org.jetbrains.kotlin:kotlin-stdlib")
            exclude("com.google.auto.service:auto-service")
        }
        onUnusedAnnotationProcessors {
            exclude("com.google.auto.service:auto-service")
        }
        onRedundantPlugins {
            exclude("kotlin-kapt")
        }
    }
}

project.afterEvaluate {
    publishing {
        publications {
            FluxCProcessorPublication(MavenPublication) {
                from components.java

                groupId "org.wordpress.fluxc"
                artifactId "fluxc-processor"
                // version is set by 'publish-to-s3' plugin
            }
        }
   }
}
