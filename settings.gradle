pluginManagement {
    gradle.ext.kotlinVersion = '1.9.22'
    gradle.ext.kspVersion = '1.9.22-1.0.17'
    gradle.ext.agpVersion = '8.5.1'
    gradle.ext.googleServicesVersion = '4.3.15'
    gradle.ext.navigationVersion = '2.7.7'
    gradle.ext.sentryVersion = '4.3.1'
    gradle.ext.daggerVersion = "2.50"
    gradle.ext.detektVersion = '1.23.0'
    gradle.ext.violationCommentsVersion = '1.70.0'
    gradle.ext.measureBuildsVersion = '3.1.0'
    gradle.ext.koverVersion = '0.7.5'
    gradle.ext.dependencyAnalysisVersion = '1.28.0'

    plugins {
        id "org.jetbrains.kotlin.android" version gradle.ext.kotlinVersion
        id "org.jetbrains.kotlin.jvm" version gradle.ext.kotlinVersion
        id "org.jetbrains.kotlin.plugin.serialization" version gradle.ext.kotlinVersion
        id "org.jetbrains.kotlin.plugin.parcelize" version gradle.ext.kotlinVersion
        id "org.jetbrains.kotlin.plugin.allopen" version gradle.ext.kotlinVersion
        id "com.android.application" version gradle.ext.agpVersion
        id "com.android.library" version gradle.ext.agpVersion
        id 'com.google.gms.google-services' version gradle.ext.googleServicesVersion
        id "androidx.navigation.safeargs.kotlin" version gradle.ext.navigationVersion
        id "io.sentry.android.gradle" version gradle.ext.sentryVersion
        id "io.gitlab.arturbosch.detekt" version gradle.ext.detektVersion
        id "se.bjurr.violations.violation-comments-to-github-gradle-plugin" version gradle.ext.violationCommentsVersion
        id 'com.automattic.android.measure-builds' version gradle.ext.measureBuildsVersion
        id "org.jetbrains.kotlinx.kover" version gradle.ext.koverVersion
        id "com.google.dagger.hilt.android" version gradle.ext.daggerVersion
        id "com.google.devtools.ksp" version gradle.ext.kspVersion
        id "com.autonomousapps.dependency-analysis" version gradle.ext.dependencyAnalysisVersion
    }
    repositories {
        maven {
            url 'https://a8c-libs.s3.amazonaws.com/android'
            content {
                includeGroup "com.automattic.android"
                includeGroup "com.automattic.android.measure-builds"
            }
        }
        gradlePluginPortal()
        google()
    }
}

plugins {
    id "com.gradle.enterprise" version "3.15"
}

rootProject.name = 'WPAndroid'

include ':WordPress'

include ':libs:image-editor'
include ':libs:networking'
include ':libs:analytics'
include ':libs:editor'
include ':libs:processors'
include ':libs:annotations'

include ':libs:mocks'

apply from: './config/gradle/included_builds.gradle'
apply from: './config/gradle/gradle_build_cache.gradle'
