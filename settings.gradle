pluginManagement {
    gradle.ext.kotlinVersion = '1.8.21'
    gradle.ext.agpVersion = '8.1.0'
    gradle.ext.googleServicesVersion = '4.3.15'
    gradle.ext.navigationVersion = '2.5.3'
    gradle.ext.sentryVersion = '3.5.0'
    gradle.ext.daggerVersion = "2.46.1"
    gradle.ext.detektVersion = '1.23.0'
    gradle.ext.violationCommentsVersion = '1.67'

    plugins {
        id "org.jetbrains.kotlin.android" version gradle.ext.kotlinVersion
        id "org.jetbrains.kotlin.jvm" version gradle.ext.kotlinVersion
        id "org.jetbrains.kotlin.kapt" version gradle.ext.kotlinVersion
        id "org.jetbrains.kotlin.plugin.serialization" version gradle.ext.kotlinVersion
        id "org.jetbrains.kotlin.plugin.parcelize" version gradle.ext.kotlinVersion
        id "org.jetbrains.kotlin.plugin.allopen" version gradle.ext.kotlinVersion
        id "com.android.application" version gradle.ext.agpVersion
        id "com.android.library" version gradle.ext.agpVersion
        id 'com.google.gms.google-services' version gradle.ext.googleServicesVersion
        id "androidx.navigation.safeargs.kotlin" version gradle.ext.navigationVersion
        id "io.sentry.android.gradle" version gradle.ext.sentryVersion
        id "io.gitlab.arturbosch.detekt" version gradle.ext.detektVersion
        id "se.bjurr.violations.violation-comments-to-github-gradle-plugin" version gradle.ext.violationCommentsVersion
    }
    repositories {
        maven {
            url 'https://a8c-libs.s3.amazonaws.com/android'
            content {
                includeGroup "com.automattic.android"
            }
        }
        gradlePluginPortal()
        google()
    }
    resolutionStrategy {
        eachPlugin {
            // TODO: Remove this when 'dagger.hilt' starts supporting Plugin Marker Artifacts
            if (requested.id.id == 'dagger.hilt.android.plugin') {
                useModule("com.google.dagger:hilt-android-gradle-plugin:$gradle.ext.daggerVersion")
            }
        }
    }
}

plugins {
    id "com.gradle.enterprise" version "3.9"
}

rootProject.name = 'WPAndroid'

include ':WordPress'

include ':libs:image-editor'
include ':libs:networking'
include ':libs:analytics'
include ':libs:editor'
include ':libs:processors'
include ':libs:annotations'

include ':libs:mocks'

apply from: './config/gradle/included_builds.gradle'
apply from: './config/gradle/gradle_build_cache.gradle'
