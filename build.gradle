buildscript {
    ext.kotlinVersion = '1.3.61'
    ext.navComponenVersion = '2.0.0'
    ext.buildGutenbergMobileJSBundle = 1

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.1'
        classpath 'com.automattic.android:fetchstyle:1.1'
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$navComponenVersion"
    }
}

plugins {
    id 'com.gradle.build-scan' version '2.0.2'
}

ext {
    buildGutenbergMobileJSBundle = 1
}

apply plugin: 'com.automattic.android.fetchstyle'

project.ext.buildGutenbergFromSource = project.properties.getOrDefault('wp.BUILD_GUTENBERG_FROM_SOURCE', false).toBoolean()
def suppressJSBundle =  System.getenv('SUPPRESS_GUTENBERG_MOBILE_JS_BUNDLE_BUILD').asBoolean()
project.ext.buildGutenbergMobileJSBundle = !(suppressJSBundle || project.ext.buildGutenbergFromSource)

if (project.ext.buildGutenbergFromSource) {
    def nodeModulesDir = new File('libs/gutenberg-mobile/node_modules')
    if (!nodeModulesDir.exists()) {
        def message = "Attempting to build Gutenberg from source without a libs/gutenberg-mobile/node_modules directory. \
Either run `npm install` manually within libs/gutenberg-mobile or disable building Gutenberg from source by \
setting wp.BUILD_GUTENBERG_FROM_SOURCE to false."
        throw new GradleException(message)
    }
}

allprojects {
    apply plugin: 'checkstyle'

    repositories {
        google()
        jcenter()
        maven { url "https://dl.bintray.com/wordpress-mobile/maven" }
        maven { url "https://jitpack.io" }

        if (rootProject.ext.buildGutenbergFromSource) {
            // nested RN libraries need the RN maven repo defined from outside so, do it here when building from source

            maven {
                // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
                url "$rootDir/libs/gutenberg-mobile/gutenberg/node_modules/react-native/android"
            }
        } else {
            maven { url "https://dl.bintray.com/wordpress-mobile/react-native-mirror/" }
        }
    }

    task checkstyle(type: Checkstyle) {
        source 'src'

        classpath = files()
    }

    checkstyle {
        toolVersion = '8.3'
        configFile file("${project.rootDir}/config/checkstyle.xml")
    }
}

subprojects {

    configurations {
        ktlint
    }

    dependencies {
        ktlint 'com.github.shyiko:ktlint:0.29.0'
    }

    task ktlint(type: JavaExec) {
        main = "com.github.shyiko.ktlint.Main"
        classpath = configurations.ktlint
        args "src/**/*.kt"
    }

    task ktlintFormat(type: JavaExec) {
        main = "com.github.shyiko.ktlint.Main"
        classpath = configurations.ktlint
        args "-F", "src/**/*.kt"
    }

    task ciktlint(type: JavaExec) {
        main = "com.github.shyiko.ktlint.Main"
        classpath = configurations.ktlint
        args "src/**/*.kt", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    }
}

buildScan {
    // Always run Gradle scan on CI builds
    if (System.getenv('CI')) {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
        tag 'CI'
        publishAlways()
    }
}

ext {
    compileSdkVersion = 28
    buildToolVersion = '28.0.3'

    minSdkVersion = 21
    targetSdkVersion = 28

    coroutinesVersion = '1.3.3'
    androidxWorkVersion = "2.0.1"

    daggerVersion = '2.22.1'
    fluxCVersion = '0880589c0'

    appCompatVersion = '1.0.2'
    constraintLayoutVersion = '1.1.3'
    materialVersion = '1.1.0'
    uCropVersion = '2.2.4'
    lifecycleVersion = '2.2.0'

    // testing
    jUnitVersion = '4.12'
    androidxTestVersion = '1.1.0'
    androidxArchCoreVersion = '2.0.0'
    assertJVersion = '3.11.1'
    espressoVersion = '3.1.0'
    mockitoCoreVersion = "2.28.2"
    nhaarmanMockitoVersion = "2.1.0"
}

// Onboarding and dev env setup tasks
task checkBundler(type:Exec) {
    doFirst {
        println "Check Bundler"
    }

    workingDir = './'
    executable "sh"
    args "-c", "if ! type 'bundle' > /dev/null; then gem install bundler; fi"

    //store the output instead of printing to the console:
    standardOutput = new ByteArrayOutputStream()

    //extension method checkBundler.output() can be used to obtain the output:
    ext.output = {
        return standardOutput.toString()
    }
}

task checkBundle(type:Exec, dependsOn:checkBundler) {
    doFirst {
        println "Check Bundle"
    }

    workingDir = './'
    executable "sh"
    args "-c", "bundle check --path=\${BUNDLE_PATH:-vendor/bundle} > /dev/null || bundle install --jobs=3 --retry=3 --path=\${BUNDLE_PATH:-vendor/bundle}"

    //store the output instead of printing to the console:
    standardOutput = new ByteArrayOutputStream()

    //extension method checkBundle.output() can be used to obtain the output:
    ext.output = {
        return standardOutput.toString()
    }
}

task applyCredentials(type:Exec, dependsOn:checkBundle) {
    doFirst {
        println "Apply credentials for this branch"
    }

    workingDir = './'
    executable "sh"
    args "-c", "FASTLANE_SKIP_UPDATE_CHECK=1 FASTLANE_ENV_PRINTER=1 bundle exec fastlane run configure_apply force:true"

    //store the output instead of printing to the console:
    standardOutput = new ByteArrayOutputStream()

    //extension method checkBundle.output() can be used to obtain the output:
    ext.output = {
        return standardOutput.toString()
    }
}

tasks.register("prodDeps") {
    group = 'Onboarding'
    description = 'Install dependencies for production builds'
    dependsOn applyCredentials
    doLast {
        println("Done")
    }
}
