import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id "io.gitlab.arturbosch.detekt"
    id "androidx.navigation.safeargs.kotlin" apply false
    id "com.android.library" apply false
    id 'com.google.gms.google-services' apply false
    id "org.jetbrains.kotlin.plugin.parcelize" apply false
}

ext {
    wordPressUtilsVersion = 'trunk-591eefcd7599adf185cc33cc4098550e2f280454'
    wordPressLoginVersion = '1.0.0'
    aztecVersion = 'v1.6.2'
    gutenbergMobileVersion = 'v1.85.0'
    storiesVersion = '2.0.0'
    aboutAutomatticVersion = '1.0.0'

    minSdkVersion = 24
    compileSdkVersion = 31
    targetSdkVersion = 31

    coroutinesVersion = '1.5.2'
    androidxWorkVersion = "2.7.0"

    fluxCVersion = 'trunk-9266d1b01317fcecf068e737e51a07186a1baf8f'

    appCompatVersion = '1.0.2'
    androidxCoreVersion = '1.3.2'
    androidxArchCoreVersion = '2.0.0'
    fragmentVersion = '1.4.1'
    constraintLayoutVersion = '1.1.3'
    materialVersion = '1.2.1'
    preferenceVersion = '1.1.0'
    swipeToRefresh = '1.1.0'
    uCropVersion = '2.2.4'
    lifecycleVersion = '2.4.1'
    tracksVersion = '2.2.0'
    roomVersion = '2.3.0'

    coreLibraryDesugaringVersion = '1.1.5'

    exoPlayerVersion = '2.13.3'

    composeVersion = '1.1.1'

    // annotations/processors
    kotlinPoetVersion = '1.6.0'
    autoServiceVersion = '1.0'

    // Apache
    commonsText = '1.10.0'
    httpClientAndroidVersion = '4.3.5.1'

    // testing
    jUnitVersion = '4.13'
    jUnitExtVersion = '1.1.3'
    androidxTestCoreVersion = '1.4.0'
    assertJVersion = '3.11.1'
    espressoVersion = '3.4.0'
    mockitoVersion = "4.8.1"
    mockitoKotlinVersion = '4.0.0'
}

allprojects {
    apply plugin: 'checkstyle'
    apply plugin: 'io.gitlab.arturbosch.detekt'

    repositories {
        google()
        mavenCentral()
        maven {
            url "https://a8c-libs.s3.amazonaws.com/android/jcenter-mirror"
            content {
                includeVersion "com.android.volley", "volley", "1.1.1"
                includeVersion "com.automattic", "rest", "1.0.8"
                includeVersion "com.google.android", "flexbox", "2.0.1"
                includeVersion "org.wordpress", "emailchecker2", "1.1.0"
                includeVersion "org.wordpress", "persistentedittext", "1.0.2"
                includeVersion "org.wordpress", "wellsql-core", "1.6.0"
                includeVersion "org.wordpress", "wellsql", "1.6.0"
                // Required for detekt
                includeVersion "org.jetbrains.kotlinx", "kotlinx-html-jvm", "0.7.2"
                // Required for lintWordpressVanillaRelease
                includeVersion "com.jraska", "falcon", "2.1.1"
            }
        }
        flatDir {
            dirs '../aars'
        }
    }

    tasks.register("checkstyle", Checkstyle) {
        source 'src'

        classpath = files()
    }

    checkstyle {
        toolVersion = '8.3'
        configFile file("${project.rootDir}/config/checkstyle.xml")
    }

    detekt {
        toolVersion = gradle.ext.detektVersion
        baseline = file("${project.rootDir}/config/detekt/baseline.xml")
        config = files("${project.rootDir}/config/detekt/detekt.yml")
        autoCorrect = false
        buildUponDefaultConfig = true
        disableDefaultRuleSets = false
        ignoreFailures = false
        parallel = true
        debug = false
    }

    tasks.withType(KotlinCompile).all {
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_1_8
            allWarningsAsErrors = true
            freeCompilerArgs += [
                    "-Xopt-in=kotlin.RequiresOptIn"
            ]
        }
    }
}

// Onboarding and dev env setup tasks
tasks.register("checkBundler", Exec) {
    doFirst {
        println "Check Bundler"
    }

    workingDir = './'
    executable "sh"
    args "-c", "if ! type 'bundle' > /dev/null; then gem install bundler; fi"

    //store the output instead of printing to the console:
    standardOutput = new ByteArrayOutputStream()

    //extension method checkBundler.output() can be used to obtain the output:
    ext.output = {
        return standardOutput.toString()
    }
}

tasks.register("checkBundle", Exec) {
    dependsOn tasks.named("checkBundler")
    doFirst {
        println "Check Bundle"
    }

    workingDir = './'
    executable "sh"
    args "-c", "bundle check --path=\${BUNDLE_PATH:-vendor/bundle} > /dev/null || bundle install --jobs=3 --retry=3 --path=\${BUNDLE_PATH:-vendor/bundle}"

    //store the output instead of printing to the console:
    standardOutput = new ByteArrayOutputStream()

    //extension method checkBundle.output() can be used to obtain the output:
    ext.output = {
        return standardOutput.toString()
    }
}

tasks.register("applyCredentials", Exec) {
    dependsOn tasks.named("checkBundle")
    doFirst {
        println "Apply credentials for this branch"
    }

    workingDir = './'
    executable "sh"
    args "-c", "FASTLANE_SKIP_UPDATE_CHECK=1 FASTLANE_ENV_PRINTER=1 bundle exec fastlane run configure_apply force:true"

    //store the output instead of printing to the console:
    standardOutput = new ByteArrayOutputStream()

    //extension method checkBundle.output() can be used to obtain the output:
    ext.output = {
        return standardOutput.toString()
    }
}

tasks.register("configureApply") {
    group = 'Onboarding'
    description = 'Install dependencies for debug and production builds'
    dependsOn applyCredentials
    doLast {
        println("Done")
    }
}

dependencies {
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:$gradle.ext.detektVersion"
}
