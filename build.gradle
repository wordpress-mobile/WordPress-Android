import com.automattic.android.measure.reporters.InternalA8cCiReporter
import com.automattic.android.measure.reporters.SlowSlowTasksMetricsReporter
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id "io.gitlab.arturbosch.detekt"
    id 'com.automattic.android.measure-builds'
    id "org.jetbrains.kotlinx.kover"
    id "com.autonomousapps.dependency-analysis"
    id "se.bjurr.violations.violation-comments-to-github-gradle-plugin" apply false
    id "androidx.navigation.safeargs.kotlin" apply false
    id "com.android.library" apply false
    id 'com.google.gms.google-services' apply false
    id "org.jetbrains.kotlin.plugin.parcelize" apply false
    id "com.google.devtools.ksp" apply false
}

ext {
    minSdkVersion = 26
    compileSdkVersion = 34
    targetSdkVersion = 34
}

ext {
    // libs
    wordPressRsVersion = 'trunk-50f703a7f677084157d02f05d4d477d7eaf960b1'
    indexosMediaForMobileVersion = '43a9026f0973a2f0a74fa813132f6a16f7499c3a'

    // main
    androidInstallReferrerVersion = '2.2'
    androidxCameraVersion = '1.3.4'
    androidxComposeBomVersion = '2024.08.00'
    androidxComposeCompilerVersion = '1.5.14'
    androidxComposeNavigationVersion = '2.7.7'
    androidxGridlayoutVersion = '1.0.0'
    androidxSwipeToRefreshVersion = '1.1.0'
    androidxWorkManagerVersion = "2.9.1"
    androidxComposeMaterial3Version = '1.2.1'
    androidxWebkitVersion =  '1.12.0'
    apacheCommonsTextVersion = '1.12.0'
    coilComposeVersion = '2.7.0'
    chrisbanesPhotoviewVersion = '2.3.0'
    eventBusVersion = '3.3.1'
    facebookShimmerVersion = '0.5.0'
    firebaseBomVersion = '33.3.0'
    firebaseIidVersion = '21.1.0'
    glideVersion = '4.16.0'
    googleAutoServiceVersion = '1.0.1'
    googleExoPlayerVersion = '2.13.3'

    googleMLKitBarcodeScanningVersion = '17.3.0'
    googleMLKitBarcodeScanningCommonVersion = '17.0.0'
    googleMLKitTextRecognitionVersion = '16.0.1'
    googlePlayServicesAuthVersion = '20.4.1'
    googlePlayServicesCodeScannerVersion = '16.0.0-beta3'
    jsoupVersion = '1.18.1'
    kotlinxCoroutinesVersion = '1.8.1'
    lottieVersion = '6.5.2'
    philjayMpAndroidChartVersion = 'v3.1.0'
    squareupRetrofitVersion = '2.11.0'
    zendeskVersion = '5.1.2'

    // android test
    androidxTestCoreVersion = '1.5.0'
    androidxTestEspressoVersion = '3.4.0'
    androidxTestExtJunitVersion = '1.2.1'
    androidxTestUiAutomatorVersion = '2.3.0'
    screengrabVersion = '2.1.1'

    // other
    androidDesugarVersion = '2.1.2'
}

measureBuilds {
    enable = findProperty('measureBuildsEnabled')?.toBoolean() ?: false
    onBuildMetricsReadyListener { report ->
        SlowSlowTasksMetricsReporter.report(report)
        InternalA8cCiReporter.reportBlocking(
                report,
                "wordpress",
                findProperty('appsMetricsToken')
        )
    }
    attachGradleScanId = System.getenv('CI')?.toBoolean() ?: false
}

allprojects {
    apply plugin: 'checkstyle'
    apply plugin: 'io.gitlab.arturbosch.detekt'

    repositories {
        google()
        mavenCentral()
        maven {
            url "https://a8c-libs.s3.amazonaws.com/android/jcenter-mirror"
            content {
                includeVersion "com.android.volley", "volley", "1.1.1"
                includeVersion "com.automattic", "rest", "1.0.8"
                includeVersion "com.google.android", "flexbox", "2.0.1"
                includeVersion "org.wordpress", "persistentedittext", "1.0.2"
                includeVersion "org.wordpress", "wellsql-core", "1.6.0"
                includeVersion "org.wordpress", "wellsql", "1.6.0"
                // Required for detekt
                includeVersion "org.jetbrains.kotlinx", "kotlinx-html-jvm", "0.7.2"
                // Required for lintWordpressVanillaRelease
                includeVersion "com.jraska", "falcon", "2.1.1"
            }
        }
    }

    tasks.register("checkstyle", Checkstyle) {
        source 'src'

        classpath = files()
    }

    checkstyle {
        toolVersion = '10.17.0'
        configFile file("${project.rootDir}/config/checkstyle.xml")
    }

    detekt {
        toolVersion = gradle.ext.detektVersion
        baseline = file("${project.rootDir}/config/detekt/baseline.xml")
        config = files("${project.rootDir}/config/detekt/detekt.yml")
        autoCorrect = false
        buildUponDefaultConfig = true
        disableDefaultRuleSets = false
        ignoreFailures = false
        parallel = true
        debug = false
    }

    tasks.withType(KotlinCompile).all {
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_1_8
            allWarningsAsErrors = true
            freeCompilerArgs += [
                    "-opt-in=kotlin.RequiresOptIn",
                    "-Xjvm-default=all"
            ]
        }
    }
}

// Onboarding and dev env setup tasks
tasks.register("checkBundler", Exec) {
    doFirst {
        println "Check Bundler"
    }

    workingDir = './'
    executable "sh"
    args "-c", "if ! type 'bundle' > /dev/null; then gem install bundler; fi"

    //store the output instead of printing to the console:
    standardOutput = new ByteArrayOutputStream()

    //extension method checkBundler.output() can be used to obtain the output:
    ext.output = {
        return standardOutput.toString()
    }
}

tasks.register("checkBundle", Exec) {
    dependsOn tasks.named("checkBundler")
    doFirst {
        println "Check Bundle"
    }

    workingDir = './'
    executable "sh"
    args "-c", "bundle check --path=\${BUNDLE_PATH:-vendor/bundle} > /dev/null || bundle install --jobs=3 --retry=3 --path=\${BUNDLE_PATH:-vendor/bundle}"

    //store the output instead of printing to the console:
    standardOutput = new ByteArrayOutputStream()

    //extension method checkBundle.output() can be used to obtain the output:
    ext.output = {
        return standardOutput.toString()
    }
}

tasks.register("applyCredentials", Exec) {
    dependsOn tasks.named("checkBundle")
    doFirst {
        println "Apply credentials for this branch"
    }

    workingDir = './'
    executable "sh"
    args "-c", "FASTLANE_SKIP_UPDATE_CHECK=1 FASTLANE_ENV_PRINTER=1 bundle exec fastlane run configure_apply force:true"

    //store the output instead of printing to the console:
    standardOutput = new ByteArrayOutputStream()

    //extension method checkBundle.output() can be used to obtain the output:
    ext.output = {
        return standardOutput.toString()
    }
}

tasks.register("configureApply") {
    group = 'Onboarding'
    description = 'Install dependencies for debug and production builds'
    dependsOn applyCredentials
    doLast {
        println("Done")
    }
}

dependencies {
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:$gradle.ext.detektVersion"
}

apply from: './config/gradle/code_coverage.gradle'
